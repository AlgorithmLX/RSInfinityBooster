buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'maven-publish'

version = "1.16.5-1.1"
def env = System.getenv()
if (env.GITHUB_RUN_NUMBER) {
    version = version + "+" + "${env.GITHUB_RUN_NUMBER}"
}

group = 'uk.co.hexeption'
archivesBaseName = 'RSInfinityBooster'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=rsinfinitybooster.mixins.json"

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=rsinfinitybooster.mixins.json"

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'rsinfinitybooster', '--all',
                    '--existing', file('src/main/resources').toString(),
                    '--existing', file('src/generated/resources').toString(),
                    '--output', file('src/generated/resources/')

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://repo.refinedmods.com/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.8'
    compileOnly fg.deobf("com.refinedmods:refinedstorage:1.9.16+199")
    runtimeOnly fg.deobf("com.refinedmods:refinedstorage:1.9.16+199")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.4:7.6.0.58")
}


jar {
    manifest {
        attributes([
                "Specification-Title"     : "rsinfinitybooster",
                //"Specification-Vendor": "rsinfinitybooster authors",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                //"Implementation-Vendor": "rsinfinitybooster authors",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "rsinfinitybooster.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

mixin {
    add sourceSets.main, "rsinfinitybooster.refmap.json"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier 'deobf'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (project.hasProperty('CURSE_API_KEY')) {
        apiKey = project.getProperty('CURSE_API_KEY')
    }
    project {
        id = '446870'
        changelog = 'A changelog can be found at https://github.com/Hexeption/RSInfinityBooster'
        releaseType = 'release'
        addGameVersion '1.16'

        mainArtifact(jar) {
            displayName = "RSInfinityBooster v$version"
        }
        relations {
            requiredDependency 'refined-storage'
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact deobfJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        if (project.hasProperty("REPO_PASSWORD")) {
            maven {
                url = "https://repo.hexeption.dev/repository/hex-mods/"
                credentials {
                    username = "admin"
                    password = project.getProperty("REPO_PASSWORD")
                }
            }
        }
    }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){
    if (project.hasProperty('MODRINTH_API_KEY')) {
        token = project.getProperty('MODRINTH_API_KEY')
    }
    projectId = 'Sjzdj2cy'
    versionName = "RSInfinityBooster v$version"
    changelog = 'A changelog can be found at https://github.com/Hexeption/RSInfinityBooster'
    versionNumber = "$version"
    uploadFile = jar
    addGameVersion("1.16.5")
    addLoader('forge')
}
